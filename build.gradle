plugins { 
    id 'java-library'  
    id 'maven-publish'    
    id 'com.diffplug.spotless' version '7.0.3'
    id 'com.gradleup.shadow' version '9.1.0'
}

spotless {
    java {
 	   target "**/*.java"		  
   	   googleJavaFormat()
    }
}    

tasks.assemble.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.shadowJar

shadowJar {                                   
    archiveFileName.set("${project.name}.jar") 
    archiveClassifier.set("") 
    
    exclude 'com/**'
    exclude 'javax/**'
    exclude 'org/apache/**'
    exclude 'org/objectweb/**'
    
    dependencies {
        exclude(dependency("com.google.code.findbugs:jsr305:3.0.2")) 
        exclude(dependency("com.google.guava:guava:33.4.6-jre")) 
        exclude(dependency("org.slf4j:slf4j-api:1.7.36")) 
     }      
}
	
dependencies { 
    implementation files("libs/remapped-gradle-tooling-api-8.14.jar")

    compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    compileOnly("com.google.guava:guava:33.4.6-jre")  
    compileOnly("org.slf4j:slf4j-api:1.7.36")
    
    compileOnly project(":build-tools")      
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->            
            groupId = 'org.kodtik.ide'
            artifactId = 'tooling-api'
            version = '8.14.4' 
         
            from(components.shadow)
        }
    }
}   
